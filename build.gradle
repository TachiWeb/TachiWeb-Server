plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.21" apply false // Also in settings.gradle
    id "java"
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.8.1"
}

allprojects {
    group = "xyz.nulldev.ts"

    version = "1.0"

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://dl.bintray.com/inorichi/maven" }
        maven { url "https://dl.google.com/dl/android/maven2/" }

        maven {
            url new File(rootProject.rootDir, "local-repo").toURI().toURL().toExternalForm()
        }
    }
}

ext.javaProjects = [
        project(":AndroidCompat"),
        project(":TachiServer"),
        project(":TachiServer/Config"),
        project(":Tachiyomi-App")
]

ext.jsProjects = [
        project(":tachiwebui")
]

configure(javaProjects) {
    apply plugin: "java"
    apply plugin: "org.jetbrains.kotlin.jvm"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    dependencies {
        // Kotlin
        compile "org.jetbrains.kotlin:kotlin-stdlib:${gradle.kotlinVersion}"
        testCompile "org.jetbrains.kotlin:kotlin-test:${gradle.kotlinVersion}"
    }
}

configure(jsProjects) {
    apply plugin: "java"
}

configure([
        project(":AndroidCompat"),
        project(":TachiServer"),
        project(":TachiServer/Config")
]) {
    dependencies {
        // Dependency Injection
        compile "com.github.salomonbrys.kodein:kodein:3.2.0"
        compile "com.github.salomonbrys.kodein:kodein-conf:3.2.0"

        // Logging
        compile "org.slf4j:slf4j-api:1.7.22"
        compile "org.slf4j:slf4j-simple:1.7.22"
        compile "io.github.microutils:kotlin-logging:1.4.3"

        // RxJava
        compile "io.reactivex:rxjava:1.2.7"
        compile "io.reactivex:rxkotlin:1.0.0"

        // JSoup
        compile "org.jsoup:jsoup:1.10.2"

        // Kotlin
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${gradle.kotlinVersion}"
        compile "org.jetbrains.kotlin:kotlin-reflect:${gradle.kotlinVersion}"
    }
}

configure([
        project(":Tachiyomi-App")
]) {
    dependencies {
        // Gson
        compile "com.google.code.gson:gson:2.8.0"
    }
}
