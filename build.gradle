plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.4-3" apply false
    id "java"
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.1"
}

allprojects {
    group = "xyz.nulldev.ts"

    version = "1.0"

    repositories {
        jcenter()
        mavenCentral()

        maven {
            url new File(rootProject.rootDir, "local-repo").toURI().toURL().toExternalForm()
        }
    }
}

ext.javaProjects = [
        project(":AndroidCompat"),
        project(":junrarandroid"),
        project(":TachiServer"),
        project(":TachiServer/SyncStruct"),
        project(":Tachiyomi-App")
]

ext.jsProjects = [
        project(":tachiwebui")
]

configure(javaProjects) {
    apply plugin: "java"
    apply plugin: "org.jetbrains.kotlin.jvm"

    dependencies {
        // Kotlin
        compile "org.jetbrains.kotlin:kotlin-stdlib:${gradle.kotlinVersion}"
        testCompile "org.jetbrains.kotlin:kotlin-test:${gradle.kotlinVersion}"
    }
}

configure(jsProjects) {
    apply plugin: "java"
}

configure([
        project(":AndroidCompat"),
        project(":TachiServer")
]) {
    dependencies {
        // Dependency Injection
        compile "com.github.salomonbrys.kodein:kodein:3.2.0"
        compile "com.github.salomonbrys.kodein:kodein-conf:3.2.0"

        // Logging
        compile "org.slf4j:slf4j-api:1.7.22"
        compile "org.slf4j:slf4j-simple:1.7.22"
        compile "io.github.microutils:kotlin-logging:1.4.3"

        // Config API
        compile "com.typesafe:config:1.3.1"

        // RxJava
        compile "io.reactivex:rxjava:1.2.7"
        compile "io.reactivex:rxkotlin:1.0.0"

        // JSoup
        compile "org.jsoup:jsoup:1.10.2"

        // Kotlin
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:${gradle.kotlinVersion}"
        compile "org.jetbrains.kotlin:kotlin-reflect:${gradle.kotlinVersion}"
    }
}
